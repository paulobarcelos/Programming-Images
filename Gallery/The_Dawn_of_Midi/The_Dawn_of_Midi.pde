// Fan art!

// DAWN OF MIDI

// by Paulo Barcelos

/*

Dawn of Midi is a band that "sounds like a computer".
It's musical language is very close to what we think
defines eletronic music. But the band is "analog" they
only play acoustic instruments...

My idea was to do a very straight forward pattern,
something that clearly was generated by a strict program,
but to incremetally add some error to the system, so that
on the end it resambles some human craft, with it's tiny
yet expressive precison mistakes.


*/

PImage title;

void setup(){
  size(1132, 700);
  background(#036564); 
    
  // Confirguration ////////////////
  float patternSize = 10;
  float minimumSquareSize = 8;
  
  float verticalMargin = 20;
  float horizontalMargin = 20;
   
  float positionErrorMax = 50;
  float positionErrorVariation = 5;
  float sizeErrorMax = 40;
  float sizeErrorVariation = 5;
  
  float errorShape = 2.5;
  //////////////////////////////////
  
  float columns = width / patternSize;
  float rows =  height / patternSize;
  float horizontalError = 0;
  float verticalError = 0;
  for(float x = 0; x < columns; x++){    
    for(float y = 0; y < rows; y++){
      float index = (x * y) / (columns * rows);
      
      float postionError = index * positionErrorMax;
      float positionVariation = positionErrorVariation * index;
      float sizeError = index * sizeErrorMax;
      float sizeVariation = sizeErrorVariation * index;
      
      noStroke();
      fill(#CDB380);
      rect(
        x * horizontalMargin + postionError + random(-positionVariation, positionVariation), 
        y * verticalMargin + postionError + random(-positionVariation, positionVariation), 
        minimumSquareSize + sizeError + random(-sizeVariation, sizeVariation),
        minimumSquareSize + sizeError + random(-sizeVariation, sizeVariation)
      );
      
      index++;
    }
  }
  
  // Draw the text on the top left corner
  fill(#036564);
  rect(15, 20, 100, 20);
  
  fill(#CDB380);
  textSize(12);
  text("DAWN OF MIDI", 20, 28); 
}

void draw(){
  

  // Save image when S is pressed
  if(keyPressed == true && key == 's'){
      save("dawn-of-midi.png");
  }
  
}


